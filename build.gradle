plugins {
    id 'java'
    id 'application'
}

if (!file("${projectDir}/dev.properties").exists()) {
    file("${projectDir}/dev.properties").write(file("${projectDir}/dev_default.properties").getText())
}

System.properties.load(new FileInputStream("dev.properties"));


group 'io.xpipe'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

// Some general java compilation settings
tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    modularity.inferModulePath = true
    options.encoding = 'UTF-8'
    // This is useful for Jackson serialization
    options.compilerArgs << '-parameters'

}

javadoc {
    source = sourceSets.main.allJava
    options {
        addStringOption('-release', '17')
        addStringOption('link', 'https://docs.oracle.com/en/java/javase/17/docs/api/')
        addBooleanOption('html5', true)
        addStringOption('Xdoclint:none', '-quiet')
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

sourceSets {
    test {
        // Disable test compilation on non IntelliJ based platforms,
        // as other ides do not like modularized tests and refuse to compile
        if (System.getProperty('idea.active') == null) {
            java {
                srcDirs = []
            }
        }

        // Fix to make resources available in tests
        output.resourcesDir("$buildDir/classes/java/test")
    }
}

// Copies all required libraries to the output directory
// Note that only libraries that are not already supplied by the X-Pipe daemon are required to be supplied
task copyRuntimeLibs(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtimeClasspath
    exclude "${project.name}.jar"
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}
jar.finalizedBy(copyRuntimeLibs)

// Junit dependencies
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

// Javafx dependencies. Not that the exact version and platform do not matter here
// as these dependencies are only used for compilation. The proper libraries for
// your platform and version are supplied by X-Pipe in the runtime environment
dependencies {
    compileOnly "org.openjfx:javafx-base:18:win"
    compileOnly "org.openjfx:javafx-graphics:18:win"
    compileOnly "org.openjfx:javafx-controls:18:win"
}

def targetVersion = System.getProperty('targetXPipeVersion')

configurations {
    launcher
}

dependencies {
    // The required X-Pipe apis
    compileOnly "io.xpipe:core:$targetVersion"
    compileOnly "io.xpipe:extension:$targetVersion"

    // Jackson annotations needed to indicate that our custom data source is serializable.
    // In case you need to perform a more complex serialization, you can also add your require Jackson libraries here.
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "2.13.0"

    // X-pipe libraries needed to communicate with the daemon from the testing environment
    testImplementation "io.xpipe:extension:$targetVersion"
    testImplementation "io.xpipe:api:$targetVersion"
    testImplementation "io.xpipe:core:$targetVersion"

    // Lombok libraries to make data source definitions more condensed
    // If you don't like to use lombok, just remove these dependencies
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

test {
    workingDir = rootDir

    // Daemon properties. The X-Pipe daemon gets launched from the testing
    // environment, so we are able to pass configuration options to it
    systemProperty "io.xpipe.beacon.daemonArgs", "-Dio.xpipe.app.mode=tray" +
            " -Dio.xpipe.beacon.port=21723" +
            " -Dio.xpipe.app.dataDir=$projectDir/local/" +
            " -Dio.xpipe.app.writeLogs=true" +
            " -Dio.xpipe.app.extensions=$buildDir/libs" +
            " -Dio.xpipe.storage.persist=false"

    // Client properties
    systemProperty 'io.xpipe.beacon.printMessages', "${System.getProperty('debugMode')}"
    systemProperty 'io.xpipe.beacon.printDaemonOutput', "${System.getProperty('debugMode')}"
    systemProperty "io.xpipe.beacon.port", "21723"
    systemProperty "io.xpipe.beacon.launchDebugDaemon", "${System.getProperty('debugMode')}"
    systemProperty "io.xpipe.beacon.attachDebuggerToDaemon", "${System.getProperty('attachDebugger')}"
}

dependencies {
    launcher "io.xpipe:beacon:$targetVersion"
    launcher "io.xpipe:api:$targetVersion"
}

run {
    classpath = project.files(classpath.files + project.configurations.launcher.files)
    mainModule = 'io.xpipe.beacon'
    mainClass = 'io.xpipe.beacon.BeaconServer'

    systemProperty "io.xpipe.beacon.daemonArgs", "-Dio.xpipe.app.mode=gui -Dio.xpipe.app.extensions=$buildDir/libs"
    systemProperty "io.xpipe.beacon.launchDebugDaemon", "${System.getProperty('debugMode')}"
    systemProperty "io.xpipe.beacon.attachDebuggerToDaemon", "${System.getProperty('attachDebugger')}"
}